################################################################################
##                                                                            ##
##  This file is part of NCrystal (see https://mctools.github.io/ncrystal/)   ##
##                                                                            ##
##  Copyright 2015-2023 NCrystal developers                                   ##
##                                                                            ##
##  Licensed under the Apache License, Version 2.0 (the "License");           ##
##  you may not use this file except in compliance with the License.          ##
##  You may obtain a copy of the License at                                   ##
##                                                                            ##
##      http://www.apache.org/licenses/LICENSE-2.0                            ##
##                                                                            ##
##  Unless required by applicable law or agreed to in writing, software       ##
##  distributed under the License is distributed on an "AS IS" BASIS,         ##
##  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  ##
##  See the License for the specific language governing permissions and       ##
##  limitations under the License.                                            ##
##                                                                            ##
################################################################################

################################################################################
#                                                                              #
# Exports NCrystal targets, and provides a few paths and values of build       #
# options.                                                                     #
#                                                                              #
# Geant4 targets will not be exported unless the GEANT4BINDINGS component is   #
# explicitly requested                                                         #
#                                                                              #
################################################################################


#Export a few directory paths (relocatable):
set( NCrystal_CMAKEDIR "${CMAKE_CURRENT_LIST_DIR}" )
get_filename_component( NCrystal_DIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2ROOT@" ABSOLUTE )
get_filename_component( NCrystal_DATAROOT "${NCrystal_DIR}/@NCrystal_DATAROOT@" ABSOLUTE )

get_filename_component( NCrystal_BINDIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2BINDIR@" ABSOLUTE )
get_filename_component( NCrystal_LIBDIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2LIBDIR@" ABSOLUTE )
get_filename_component( NCrystal_INCDIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2INCDIR@" ABSOLUTE )
if ( @NCRYSTAL_ENABLE_PYTHON@ AND NOT @NCRYSTAL_SKIP_PYMODINST@ AND EXISTS "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2PYPATH@/NCrystal/__init__.py" )
  get_filename_component( NCrystal_PYPATH "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2PYPATH@" ABSOLUTE )
endif()
if ( "x@NCRYSTAL_ENABLE_DATA@" STREQUAL "xON" )
  get_filename_component( NCrystal_DATAFILESDIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2DATAFILESDIR@" ABSOLUTE )
  set( NCrystal_OPTION_INSTALL_DATA        ON )
else()
  set( NCrystal_DATAFILESDIR "")
  set( NCrystal_OPTION_INSTALL_DATA        OFF )
endif()

if ( "x@NCRYSTAL_ENABLE_DATA@" STREQUAL "xEMBED" )
  set( NCrystal_OPTION_EMBED_DATA          ON )
else()
  set( NCrystal_OPTION_EMBED_DATA          OFF )
endif()

#Config variables (backwards compatible names):
set( NCrystal_OPTION_BUILD_EXAMPLES      @NCRYSTAL_ENABLE_EXAMPLES@ )
set( NCrystal_OPTION_BUILD_G4HOOKS       @NCRYSTAL_GEANT4@ )
set( NCrystal_OPTION_INSTALL_MCSTAS      @NCRYSTAL_ENABLE_MCSTAS@ )
set( NCrystal_OPTION_INSTALL_PY          @NCRYSTAL_ENABLE_PYTHON@ )
set( NCrystal_OPTION_INSTALL_SETUPSH     @NCRYSTAL_ENABLE_SETUPSH@ )
set( NCrystal_OPTION_MODIFY_RPATH        @NCRYSTAL_MODIFY_RPATH@ )
set( NCrystal_OPTION_NO_DIRECT_PYMODINST @NCRYSTAL_SKIP_PYMODINST@ )

#Config variables (autogenerated list):
set( _tmpnc_opts "@_NCrystal_all_opts@" )
set( _tmpnc_optsvals "@_NCrystal_all_opts_vals@" )
while( _tmpnc_opts )
  list( POP_FRONT _tmpnc_opts _tmpnc_o )
  list( POP_FRONT _tmpnc_optsvals _tmpnc_v )
  string( REPLACE "NCRYSTAL_" "NCrystal_OPT_" _tmpnc_o "${_tmpnc_o}" )
  set( "${_tmpnc_o}" "${_tmpnc_v}" )
endwhile()
unset( _tmpnc_opts )
unset( _tmpnc_optsvals )
unset( _tmpnc_o )
unset( _tmpnc_v )

#Libname:
set( NCrystal_LIBNAME @NCrystal_LIBNAME@ )

#Various scripts:
if ( NCrystal_OPTION_INSTALL_PY )
  set( NCrystal_CMD_NCMAT2CPP ${NCrystal_BINDIR}/ncrystal_ncmat2cpp )
  if ( NOT EXISTS NCrystal_NCMAT2CPP )
    set( NCrystal_NCMAT2CPP "")
  endif()
  set( NCrystal_CMD_NCRYSTALCONFIG ${NCrystal_BINDIR}/ncrystal-config )
  if ( NOT EXISTS NCrystal_CMD_NCRYSTALCONFIG )
    set( NCrystal_CMD_NCRYSTALCONFIG "")
  endif()
  set( NCrystal_CMD_NCRYSTALINSPECTFILE ${NCrystal_BINDIR}/ncrystal_inspectfile )
  if ( NOT EXISTS NCrystal_CMD_NCRYSTALINSPECTFILE )
    set( NCrystal_CMD_NCRYSTALINSPECTFILE "")
  endif()
  set( NCrystal_CMD_NCTOOL ${NCrystal_BINDIR}/nctool )
  if ( NOT EXISTS NCrystal_CMD_NCTOOL )
    set( NCrystal_CMD_NCTOOL "")
  endif()
endif()

#The NCrystal targets (not including the G4NCrystal targets!):
if(NOT TARGET NCrystal::NCrystal)
  include( "${NCrystal_CMAKEDIR}/NCrystalTargets.cmake" )
endif()

#For now GEANT4BINDINGS is the only optional component. To avoid injecting a
#dependency on Geant4 into non-Geant4 projects, the Geant4 dependency and G4NCrystal
#targets will only be added if the GEANT4BINDINGS component is explicitly
#requested.

set( NCrystal_COMPONENT_GEANT4BINDINGS OFF )

if ( NCrystal_OPTION_BUILD_G4HOOKS )
  #Build with Geant4 bindings. However, only load these targets (and the Geant4
  #dependency) if the GEANT4BINDINGS component was requested.
  if ( "GEANT4BINDINGS" IN_LIST NCrystal_FIND_COMPONENTS )
    if( NOT TARGET NCrystal::G4NCrystal )
      include( CMakeFindDependencyMacro )
      find_dependency( Geant4 @Geant4_VERSION@ EXACT REQUIRED )
      include( "${NCrystal_CMAKEDIR}/G4NCrystalTargets.cmake" )
      set( NCrystal_COMPONENT_GEANT4BINDINGS ON )
    endif()
  endif()
else()
  if ( NCrystal_FIND_REQUIRED_GEANT4BINDINGS )
    #GEANT4BINDINGS were explicitly requested but this installation does not
    #support it.
    set( NCrystal_FOUND FALSE )
  endif()
endif()

#Handle other requested components. For forward compatibility, silently ignore
#any requested component name we do not recognise, unless it is REQUIRED:
foreach(tmp ${NCrystal_FIND_COMPONENTS})
  if ( NCrystal_FIND_REQUIRED_${tmp} AND NOT "x${tmp}" STREQUAL "xGEANT4BINDINGS" )
    set( NCrystal_FOUND FALSE )
  endif()
endforeach()
